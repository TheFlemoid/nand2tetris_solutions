// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // This is broken down left to right, as seen in the ALU specification
    // seen on page 37.  Each stage basically incorporates us performing both
    // of the specified actions (eg. zeroing x and also not zeroing x), and then
    // using a mux to determine what result was actually desired.

    // zx and zy signals
    And16(a=x, b=false, out=zero);
    Mux16(a=x, b=zero, sel=zx, out=xmux1);
    Mux16(a=y, b=zero, sel=zy, out=ymux1);

    // nx and ny signals
    Not16(in=xmux1, out=notx);
    Mux16(a=xmux1, b=notx, sel=nx, out=xmux2);
    Not16(in=ymux1, out=noty);
    Mux16(a=ymux1, b=noty, sel=ny, out=ymux2);

    // Function
    And16(a=xmux2, b=ymux2, out=andxy);
    Add16(a=xmux2, b=ymux2, out=addxy);
    Mux16(a=andxy, b=addxy, sel=f, out=functionout);

    // Notting the output
    Not16(in=functionout, out=notfunctionout);
    Mux16(a=functionout, b=notfunctionout, sel=no, out=out, out[15]=ng, out[0..7]=zr1, out[8..15]=zr2);

    // Zero register flag, true if output equals zero
    Or8Way(in=zr1, out=or8way1);
    Or8Way(in=zr2, out=or8way2);
    Or(a=or8way1, b=or8way2, out=notzero);
    Not(in=notzero, out=zr);
}
